apply plugin: 'groovy'

description 'Gradle plugin for running SoapUI projects'
group 'com.lv.plugins'
version = '0.1'

repositories {
    if( project.hasProperty('internalArtifactRepository') ){
        maven { url project.internalArtifactRepository }
    } else {
        maven { url 'http://www.eviware.com/repository/maven2/' }
        mavenCentral()
        jcenter()
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    /**
     * SoapUI dependencies
     */
    //compile files('D:\\tools\\SoapUI-5.0.0\\bin\\soapui-5.0.0.jar')
    //compile fileTree(dir: "D:\\tools\\SoapUI-5.0.0\\lib", include: '*.jar')

    // This is really horrible and hacky, but it seems the only way to resolve
    // the dependencies needed by SoapUI
    compile ('eviware:maven-soapui-plugin:4.5.0') {
        exclude( module: 'jtidy' )
    }

    testCompile 'com.netflix.nebula:nebula-test:1.12.2'
}

sourceSets {
    integTest {
        java.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

check.dependsOn integTest
